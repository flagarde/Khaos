cmake_minimum_required(VERSION 3.0...${CMAKE_VERSION} FATAL_ERROR)

if("${CMAKE_VERSION}" VERSION_LESS "3.12")
  file(DOWNLOAD "https://cmake-tools.github.io/cmmm/latest/GetCMakeMM.cmake" "${CMAKE_CURRENT_BINARY_DIR}/GetCMakeMM.cmake")
  include("${CMAKE_CURRENT_BINARY_DIR}/GetCMakeMM.cmake")
else()
  include(FetchContent)
  FetchContent_Declare(cmmm GIT_REPOSITORY https://github.com/cmake-tools/cmmm.git GIT_TAG main)
  if("${CMAKE_VERSION}" VERSION_LESS "3.15")
    FetchContent_GetProperties(cmmm)
    if(NOT cmmm_POPULATED)
      FetchContent_Populate(cmmm)
      add_subdirectory("${cmmm_SOURCE_DIR}" "${cmmm_BINARY_DIR}")
    endif()
  else()
    FetchContent_MakeAvailable(cmmm)
  endif()
endif()

if("${CMAKE_VERSION}" VERSION_LESS "3.9")
    project(Khaos VERSION "0.0.0.0" LANGUAGES NONE)
elseif("${CMAKE_VERSION}" VERSION_LESS "3.12")
    project(Khaos VERSION "0.0.0.0" DESCRIPTION "A C/C++ library to simplify cross-platform programming" LANGUAGES NONE)
else()
    project(Khaos VERSION "0.0.0.0" DESCRIPTION "A C/C++ library to simplify cross-platform programming" HOMEPAGE_URL "https://github.com/flagarde/Khaos" LANGUAGES NONE)
endif()

cmmm(VERSION latest)
cmmm_modules_list(URL "gh:cmake-tools/cmmm.test" DESTINATION "${CMAKE_CURRENT_BINARY_DIR}/modulesList2")

include(CMakeDependentOption)
if(CMAKE_VERSION VERSION_LESS 3.21)
  get_property(Khaos_IS_TOP_LEVEL DIRECTORY PROPERTY PARENT_DIRECTORY)
  if(Khaos_IS_TOP_LEVEL)
    set(Khaos_IS_TOP_LEVEL FALSE)
  else()
    set(Khaos_IS_TOP_LEVEL TRUE)
  endif()
endif()
cmake_dependent_option(KHAOS_ENABLE_TESTS "Build the tests" ON "Khaos_IS_TOP_LEVEL" OFF)
cmake_dependent_option(KHAOS_ENABLE_EXAMPLES "Build the examples" ON "Khaos_IS_TOP_LEVEL" OFF)
cmake_dependent_option(KHAOS_ENABLE_DOCS "Build the documentations" ON "Khaos_IS_TOP_LEVEL" OFF)

add_subdirectory(src)

if(KHAOS_ENABLE_TESTS)
  add_subdirectory(tests)
endif()

if(KHAOS_ENABLE_EXAMPLES)
  add_subdirectory(examples)
endif()

if(KHAOS_ENABLE_DOCS)
  add_subdirectory(docs)
endif()
